# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=liri-calamares-branding-git
pkgver=20180310.94ab125
pkgrel=1
pkgdesc="Liri OS branding and customizations for Calamares"
arch=('any')
url='https://liri.io'
license=('GPL3')
depends=('os-prober')
makedepends=('git' 'liri-qbs-shared-git')
conflicts=('liri-calamares-branding')
replaces=('liri-calamares-branding')
provides=('liri-calamares-branding')
groups=('liri-git')

_gitroot="git://github.com/lirios/calamares-branding.git"
_gitbranch=develop
_gitname=calamares-branding
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
	mkdir -p build
}

build() {
	cd ${srcdir}/${_gitname}
	qbs setup-toolchains --type gcc /usr/bin/g++ gcc
	qbs setup-qt /usr/bin/qmake-qt5 qt5
	qbs config profiles.qt5.baseProfile gcc
	qbs build --no-install -d build profile:qt5 \
		modules.lirideployment.prefix:/usr
}

package() {
	cd ${srcdir}/${_gitname}
	qbs install -d build --no-build -v --install-root $pkgdir profile:qt5
}
