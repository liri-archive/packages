# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=liri-calamares-branding-git
pkgver=20161013.1030554
pkgrel=1
pkgdesc="Liri OS branding and customizations for Calamares"
arch=('any')
url='http://liri.io'
license=('GPL3')
depends=('os-prober')
makedepends=('git' 'extra-cmake-modules')
conflicts=('liri-calamares-branding')
replaces=('liri-calamares-branding')
provides=('liri-calamares-branding')
groups=('liri-git')

_gitroot="git://github.com/lirios/calamares-branding.git"
_gitbranch=develop
_gitname=calamares-branding
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_INSTALL_LIBDIR=lib \
		-DKDE_INSTALL_LIBEXECDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
