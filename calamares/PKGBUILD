# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

_tag=3.1.4

pkgname=calamares
pkgver=3.1.4
pkgrel=2
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=('GPL3')
url="https://calamares.io"
depends=('qt5-svg' 'qt5-webengine' 'kconfig' 'kcoreaddons' 'ki18n' 'solid' 'yaml-cpp'
         'parted' 'libatasmart' 'udisks2' 'polkit-qt5' 'kpmcore>=3.1.0' 'kparts'
         'python' 'boost-libs')
makedepends=('git' 'extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'boost')
options=('!staticlibs')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')
source=("https://github.com/calamares/calamares/releases/download/v${_tag}/calamares-${pkgver}.tar.gz")
sha256sums=('8255eb9b001b9b1271771c1796eb8217be78299c0a4e7f20e432b3dd4f5d371f')

prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DWITH_CRASHREPORTER:BOOL=OFF
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install

	cd ../${pkgname}-${pkgver}
	install -Dm644 "settings.conf" "$pkgdir/usr/share/calamares/settings.conf"
	install -Dm644 "calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
}
