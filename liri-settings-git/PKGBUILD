# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=liri-settings-git
pkgver=20170921.088a671
pkgrel=1
pkgdesc="Settings application and modules for Liri OS"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://liri.io'
license=('GPL3')
depends=('vibe-git' 'liri-wayland-git' 'libqtxdg' 'qt5-accountsservice-git' 'polkit-qt5' 'xkeyboard-config')
makedepends=('git' 'liri-qbs-shared-git')
options=(debug !strip)
conflicts=('liri-settings')
replaces=('liri-settings')
provides=('liri-settings')
groups=('liri-git')

_gitroot="git://github.com/lirios/settings.git"
_gitbranch=develop
_gitname=settings
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
	cd ${srcdir}/${_gitname}
	git submodule update --init
}

build() {
	cd ${srcdir}/${_gitname}
	qbs setup-toolchains --type gcc /usr/bin/g++ gcc
	qbs setup-qt /usr/bin/qmake-qt5 qt5
	qbs config profiles.qt5.baseProfile gcc
	qbs build --no-install -d build profile:qt5 qbs.installRoot:/ qbs.installPrefix:usr modules.lirideployment.qmlDir:lib/qt/qml
}

package() {
	cd ${srcdir}/${_gitname}
	qbs install -d build --no-build -v --install-root $pkgdir profile:qt5
}
